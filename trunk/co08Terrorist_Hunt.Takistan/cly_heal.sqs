;CLY Heal - a self-bandaging script written by Celery.
;
;-How to use-
;Execute in init script: [player,0.2,0.15,3,true] exec "cly_heal.sqs";
;The first number is the damage threshold after which you are given the option to heal.
;The second one is the player's damage after healing.
;The third one is the number of uses. -1 is unlimited.
;The boolean at the end is optional, setting it to true will make the bandaging action
;pop up when the player is injured. It's false by default.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

_target=_this select 0
_caller=_this select 1
_id=_this select 3
?typeName _id=="SCALAR":goto "init"
goto _id
exit



#init
?!local _target:exit
_healthreshold=_this select 1
_healdamage=_this select 2
_healings=floor (_this select 3)
_popup=_this select 4
?isNil {_popup}:_popup=false
CLY_healanims=["amovpknlmstpsraswrfldnon_ainvpknlmstpslaywrfldnon","ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpslaywrfldnon_amovpknlmstpsraswrfldnon","amovpknlmstpsraswpstdnon_ainvpknlmstpsnonwnondnon","amovpknlmstpsraswpstdnon_ainvpknlmstpsnonwnondnon_end","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_amovpknlmstpsraswpstdnon","amovpknlmstpsraswlnrdnon_ainvpknlmstpsnonwnondnon","ainvpknlmstpsnonwnondnon_medic_2","amovpknlmstpsnonwnondnon_amovpknlmstpsraswlnrdnon"]

;Start of unit's life
#start
@alive player
_unit=player

?isNil {_unit getVariable "CLY_healings"}:_unit setVariable ["CLY_healings",_healings,true]
?isNil {_unit getVariable "CLY_healthreshold"}:_unit setVariable ["CLY_healthreshold",_healthreshold]
?isNil {_unit getVariable "CLY_healdamage"}:_unit setVariable ["CLY_healdamage",_healdamage]
?isNil {_unit getVariable "CLY_healcooldown"}:_unit setVariable ["CLY_healcooldown",time-5]
?isNil {_unit getVariable "CLY_healinterrupt"}:_unit setVariable ["CLY_healinterrupt",false]

_healaction=100
_interruptaction=100
_healotheraction=100
_giveaction=100
_takeaction=100
_nearestaliveactive=objNull
_nearestdeadactive=objNull

;Reset actions
#reset
_unit removeAction _healaction
_unit removeAction _interruptaction
_nearestaliveactive removeAction _healotheraction;_nearestaliveactive removeAction _giveaction;_nearestaliveactive=objNull
_nearestdeadactive removeAction _takeaction;_nearestdeadactive=objNull

_healings=_unit getVariable "CLY_healings"
_leftstring=""
?_healings>0:_leftstring=format [" (%1 left)",_unit getVariable "CLY_healings"]
_healaction=_unit addAction [format ["Bandage wounds%1",_leftstring],"cly_heal.sqs","heal",1.4,_popup,true,"","_this==_target and (damage _this>_this getVariable 'CLY_healthreshold' or !canStand _this) and !(animationState _this in CLY_healanims) and time>_this getVariable 'CLY_healcooldown' and _this getVariable 'CLY_healings'!=0"]
_interruptaction=_unit addAction ["Cancel bandaging","cly_heal.sqs","interrupt",1.4,true,true,"","_this==_target and animationState _target in ['ainvpknlmstpslaywrfldnon_medic','ainvpknlmstpsnonwnondnon_medic_1','ainvpknlmstpsnonwnondnon_medic_2']"]

#loop
?!alive _unit or _unit!=player:_unit removeAction _healaction;_unit removeAction _interruptaction;_nearestaliveactive removeAction _giveaction;_nearestdeadactive removeAction _takeaction;goto "start"
?_unit getVariable "CLY_healings"!=_healings:goto "reset"

_nearestalive=objNull;_nearestdead=objNull;_nearestalivedist=5;_nearestdeaddist=5
{if (_x distance _unit<_nearestalivedist and alive _x and _x!=_unit and _unit countEnemy [_x]==0) then {_nearestalive=_x;_nearestalivedist=_x distance _unit};if (_x distance _unit<_nearestdeaddist and !alive _x and !isNil {_x getVariable "CLY_healings"} and _x!=_unit) then {_nearestdead=_x;_nearestdeaddist=_x distance _unit}} forEach allUnits+allDead
?_nearestalive!=_nearestaliveactive:_nearestaliveactive removeAction _healotheraction;_nearestaliveactive removeAction _giveaction;if (isNull _nearestalive) then {_nearestaliveactive=objNull}
?_nearestdead!=_nearestdeadactive:_nearestdeadactive removeAction _takeaction;if (isNull _nearestdead) then {_nearestdeadactive=objNull}
?!isNull _nearestalive and _nearestalive!=_nearestaliveactive:_nearestaliveactive=_nearestalive;_healotheraction=_nearestaliveactive addAction [format ["Bandage %1%2",name _nearestaliveactive,_leftstring],"cly_heal.sqs","healother",1.39,_popup,true,"","_this!=_target and (damage _target>_this getVariable 'CLY_healthreshold' or !canStand _target) and !(animationState _this in CLY_healanims) and time>_this getVariable 'CLY_healcooldown' and _this getVariable 'CLY_healings'!=0 and _this distance _target<3"];_giveaction=_nearestaliveactive addAction [format ["Give bandage to %1%2",name _nearestaliveactive,_leftstring],"cly_heal.sqs","give",1.37,false,false,"","_this!=_target and alive _target and !(animationState _this in CLY_healanims) and time>_this getVariable 'CLY_healcooldown' and _target getVariable 'CLY_healings'>=0 and _this getVariable 'CLY_healings'>0 and _this distance _target<3"]
?!isNull _nearestdead and _nearestdead!=_nearestdeadactive:_nearestdeadactive=_nearestdead;_takeaction=_nearestdeadactive addAction ["Take bandages","cly_heal.sqs","take",1.38,true,false,"","_this!=_target and _target getVariable 'CLY_healings'>0 and _this getVariable 'CLY_healings'>=0 and _this distance _target<3"]
~0.1
goto "loop"



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#heal
_weapontype=getNumber (configFile/"CfgWeapons"/(currentWeapon _caller)/"type")
_weapontypes=[];{_weapontypes=_weapontypes+[getNumber (configFile/"CfgWeapons"/_x/"type")]} forEach weapons _caller

_anim="ainvpknlmstpsnonwnondnon_medic_2"
?_weapontype==2 or (_weapontype==0 and 2 in _weapontypes):_anim="ainvpknlmstpsnonwnondnon_medic_1"
?_weapontype==1 or _weapontype==5 or (_weapontype==0 and {_x in [1,5]} count _weapontypes>0):_anim="ainvpknlmstpslaywrfldnon_medic"
_caller playMove _anim
_caller setVariable ["CLY_healcooldown",time+5]
_caller setVariable ["CLY_healinterrupt",false]

@(animationState _caller in ["ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_medic_2"])
@!(animationState _caller in ["ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_medic_2"]) or _caller getVariable "CLY_healinterrupt"
?!alive _caller:exit
?_caller getVariable "CLY_healinterrupt":goto "interruptheal"


?damage _caller>_caller getVariable "CLY_healthreshold":_caller setDamage (_caller getVariable "CLY_healdamage");_caller setVariable ["CLY_healings",(_caller getVariable "CLY_healings")-1,true]
?!canStand _caller:_caller setHit ["legs",CLY_healdamage];_caller setVariable ["CLY_healings",(_caller getVariable "CLY_healings")-1,true]
exit
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#healother
_weapontype=getNumber (configFile/"CfgWeapons"/(currentWeapon _caller)/"type")

_anim="ainvpknlmstpsnonwnondnon_medic_2"
?_weapontype==1 or _weapontype==5:_anim="ainvpknlmstpslaywrfldnon_medic"
?_weapontype==2:_anim="ainvpknlmstpsnonwnondnon_medic_1"
_caller playMove _anim
_caller setVariable ["CLY_healcooldown",time+5]
_caller setVariable ["CLY_healinterrupt",false]

@(animationState _caller in ["ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_medic_2"])
@!(animationState _caller in ["ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_medic_2"]) or !alive _target or _caller distance _target>3 or _caller getVariable "CLY_healinterrupt"
?!alive _caller:exit
?!alive _target or _caller distance _target>3 or _caller getVariable "CLY_healinterrupt":goto "interruptheal"

?damage _target>_caller getVariable "CLY_healthreshold":_target setDamage (_caller getVariable "CLY_healdamage");_caller setVariable ["CLY_healings",(_caller getVariable "CLY_healings")-1,true]
?!canStand _target:_target setHit ["legs",CLY_healdamage];_target setVariable ["CLY_healings",(_target getVariable "CLY_healings")-1,true]
exit
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#interruptheal
_caller setVariable ["CLY_healinterrupt",false]
?_weapontype==1 or _weapontype==5:_caller setVehicleInit "this switchMove 'ainvpknlmstpslaywrfldnon_amovpknlmstpsraswrfldnon';this playMoveNow 'ainvpknlmstpslaywrfldnon_amovpknlmstpsraswrfldnon'";processInitCommands;exit
?_weapontype==2:_caller setVehicleInit "this switchMove 'ainvpknlmstpsnonwnondnon_amovpknlmstpsraswpstdnon';this playMoveNow 'ainvpknlmstpsnonwnondnon_amovpknlmstpsraswpstdnon'";processInitCommands;exit
?_weapontype==4:_caller setVehicleInit "this switchMove 'amovpknlmstpsnonwnondnon_amovpknlmstpsraswlnrdnon';this playMoveNow 'amovpknlmstpsnonwnondnon_amovpknlmstpsraswlnrdnon'";processInitCommands;exit
_caller setVehicleInit "this switchMove 'ainvpknlmstpslaywrfldnon_amovpknlmstpsnonwnondnon';this playMoveNow 'ainvpknlmstpslaywrfldnon_amovpknlmstpsnonwnondnon'";processInitCommands
exit
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#give
_caller setVariable ["CLY_healings",(_caller getVariable "CLY_healings")-1,true]
_target setVariable ["CLY_healings",(_target getVariable "CLY_healings")+1,true]
exit
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#take
_taken=_target getVariable "CLY_healings"
_caller setVariable ["CLY_healings",(_caller getVariable "CLY_healings")+_taken,true]
_target setVariable ["CLY_healings",0,true]
_text=" bandage taken"
?_taken>1:_text=" bandages taken"
titleText [format ["%1%2",_taken,_text],"PLAIN DOWN",0.5]
exit
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#interrupt
_caller setVariable ["CLY_healinterrupt",true]
exit
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;